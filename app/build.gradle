apply plugin: 'com.android.application'


android {
    compileSdkVersion versions.compileSdk

    compileOptions {
        sourceCompatibility versions.java
        targetCompatibility versions.java
    }

    signingConfigs {
        config {
            storeFile file(signing.storeFile)
            storePassword signing.storePassword
            keyAlias signing.keyAlias
            keyPassword signing.keyPassword
        }
    }

    defaultConfig {
        applicationId "com.wptdxii.playground"
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionCode versions.code
        versionName versions.name
        signingConfig signingConfigs.config
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }


    buildTypes {
        debug {
            applicationIdSuffix '.debug'
        }

        release {
            postprocessing {
                removeUnusedCode true
                removeUnusedResources true
                obfuscate true
                optimizeCode true
                proguardFile 'proguard-rules.pro'
            }
        }
    }

    productFlavors {
        flavorDimensions "default"

        mock {
            dimension "default"
        }

        prod {
            dimension "default"
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            outputFileName = "Playground-v${variant.versionName}.apk"
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':sdk:framekit')
    implementation project(':sdk:uikit')
    implementation support.v7
    implementation support.constraint
    implementation support.design
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    testImplementation test.junit
    androidTestImplementation test.runner
    androidTestImplementation test.espresso

    implementation di.butterknife
    annotationProcessor di.butterknifeProcessor
}
