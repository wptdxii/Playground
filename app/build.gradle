apply plugin: 'com.android.application'
apply plugin: 'channel'

android {
    compileSdkVersion versions.compileSdk

    compileOptions {
        sourceCompatibility versions.java
        targetCompatibility versions.java
    }

    signingConfigs {
        config {
            storeFile file(signing.storeFile)
            storePassword signing.storePassword
            keyAlias signing.keyAlias
            keyPassword signing.keyPassword
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }

    defaultConfig {
        applicationId "com.wptdxii.playground"
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionCode versions.code
        versionName versions.name
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            signingConfig signingConfigs.config
        }

        release {
            signingConfig signingConfigs.config
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
//            postprocessing {
//                removeUnusedCode true
//                removeUnusedResources true
//                obfuscate true
//                optimizeCode true
//                proguardFile 'proguard-rules.pro'
//            }
        }
    }

    productFlavors {
        flavorDimensions "default"

        mock {
            dimension "default"
        }

        prod {
            dimension "default"
        }

    }

    variantFilter { variant ->
        if ('release' == variant.buildType.name
                && 'mock' == variant.getFlavors().get(0).name) {
            variant.setIgnore(true)
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            outputFileName = "Playground-v${variant.versionName}.apk"
        }
    }

    // for VasDolly
    channel {
        channelFile = file(channels.file)
        apkNameFormat = 'Playground-v${versionName}-${flavorName}-${buildType}'
        isFastMode = false
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':sdk:framekit')
    implementation project(':sdk:uikit')
    implementation support.v7
    implementation support.constraint
    implementation support.design
    implementation support.cardView
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    testImplementation test.junit
    androidTestImplementation test.runner
    androidTestImplementation test.espresso

    implementation di.butterknife
    annotationProcessor di.butterknifeCompiler

    implementation room.runtime
    annotationProcessor room.compiler
    testImplementation room.test
//    implementation room.rxjava

    implementation channels.vasdolly
}
